/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
}

description = 'Ballerina - Jaeger Extension - Native Module'

dependencies {
    implementation "org.ballerinalang:ballerina-runtime:${ballerinaLangVersion}"
    implementation "org.ballerinalang:ballerina-config:${ballerinaLangVersion}"
    implementation "io.jaegertracing:jaeger-core:${jaegerVersion}"
    implementation "io.jaegertracing:jaeger-thrift:${jaegerVersion}"
    implementation "org.apache.thrift:libthrift:${libThriftVersion}"
    implementation "com.squareup.okhttp3:okhttp:${okHttpVersion}"
    implementation "com.squareup.okio:okio:${okIoVersion}"
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

jar {
    manifest {
        attributes('Implementation-Title': project.name, 'Implementation-Version': project.version)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId "jaeger-extension-native"
            version = project.version
            artifact jar
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/${System.getenv("packagePublishRepo")}")
            credentials {
                username = System.getenv("packageUser")
                password = System.getenv("packagePAT")
            }
        }
    }
}

def tracerPropertiesFile = new File("${project.projectDir}/src/main/resources/jaeger-tracer.properties")
def originalProperties = tracerPropertiesFile.text

task updateReporterProperties {
    doLast {
        def tomlVersion = project.version.split("-")[0]

        def newProperties = tracerPropertiesFile.text.replace("@toml.version@", tomlVersion)
        tracerPropertiesFile.text = newProperties
    }
}

task revertReporterProperties {
    doLast {
        tracerPropertiesFile.text = originalProperties
    }
}

processResources.finalizedBy revertReporterProperties
processResources.dependsOn updateReporterProperties
